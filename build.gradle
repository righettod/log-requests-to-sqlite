buildscript {
    ext{
        odcPluginVersion = project.hasProperty('odcGradlePluginVersion') ? project.getProperty('odcGradlePluginVersion') : '5.2.0'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.owasp:dependency-check-gradle:$odcPluginVersion"
    }
}

apply plugin: 'java'
apply plugin: 'org.owasp.dependencycheck'

group 'eu.righettod'
version '1.0.8'
def burpExtensionHomepage = 'https://github.com/righettod/log-requests-to-sqlite'
def burpExtensionJarName = 'LogRequestsToSQLite.jar'

repositories {
    mavenCentral()
}

dependencies {
    compile 'org.xerial:sqlite-jdbc:3.28.0'
    compile 'net.portswigger.burp.extender:burp-extender-api:2.1'
}

sourceSets {
    main {
        java {
            srcDir 'src'
        }
        resources {
            srcDir 'resources'
        }
    }
}

compileJava {
    targetCompatibility '1.8'
    sourceCompatibility '1.8'
}

dependencyCheck {
    autoUpdate = true
    failOnError = true
    failBuildOnCVSS = 1
    cveValidForHours = 12
    suppressionFile = "odc-suppress.xml"
    format = 'JSON'
}

task fatJar(type: Jar) {
    manifest {
        attributes("Implementation-Version": project.version, "Implementation-Title": project.name, "Implementation-URL": burpExtensionHomepage)
    }
    archiveName = burpExtensionJarName
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}
